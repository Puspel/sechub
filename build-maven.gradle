// SPDX-License-Identifier: MIT
/* ============================================================================
   This file contains the configurations for
   Maven settings
   ============================================================================

   Included from: "${rootProject.projectDir}/build.gradle"
   ============================================================================
 */

subprojects {

	apply plugin: 'maven'

	group = "com.daimler.sechub"
    

    if (!projectType.publishedLibraries.contains(project)){
        return;
    }

    /* ensure we are at GITHUB actions when we want to publish the libraries */
    if (! buildDoneByGitHubActions ){
        println("library $project.name publish will be ignored - only provided by github actions")
        return;
    }
    
    version = VersionData.getLibrariesVersion()

	apply plugin: 'maven-publish'
	
	publishing {

      repositories {
        maven {
          name = "GitHubPackages"
          
          url = "https://maven.pkg.github.com/daimler/sechub"
          credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
          }
        }
      }
      
      publications {
           mavenJava(MavenPublication) {

           from components.java

           pom {
               name = "${project.name}:${project.version}"
               description = 'This artifact is part of SecHub'

               scm {
                   url = 'https://github.com/daimler/sechub'
               }

                licenses {
                   license {
                       name = 'MIT License'
                       url = 'https://github.com/daimler/sechub/blob/master/LICENSE'
                   }
               }
           }
      }
      }
      
    }
    
}
